plugins {
    id 'com.android.application'
}

buildDir = '../../../build/android'

android {
    namespace "org.libsdl.app"
    compileSdkVersion 36
    ndkVersion "28.2.13676358"
    ndkPath "/Users/metin/Libraries/AndroidNDK/AndroidNDK13676358.app/Contents/NDK"

    defaultConfig {
        applicationId "org.libsdl.app"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                if (project.hasProperty('chapterToBuild') && !project.property('chapterToBuild').empty) 
                {
                    arguments "-DBUILD_STANDALONE=ON"
                    arguments "-DSTANDALONE_CHAPTER_NAME=${project.property('chapterToBuild')}"
                    targets "main"
                } 
                else 
                {
                    arguments "-DBUILD_STANDALONE=OFF"
                    targets "main" 
                }
                abiFilters 'x86_64','arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    externalNativeBuild {
        cmake {
            path "../../../CMakeLists.txt"
            buildStagingDirectory = project.buildDir
        }
    }

    sourceSets.main {
        assets.srcDirs = ['../../../assets']
    }

    aaptOptions {
        noCompress ".glsl", ".vert", ".frag"
    }

    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}